function(project_build_tools_optimize_sources)
  if(${CMAKE_CXX_COMPILER_ID} MATCHES "GNU|Clang|AppleClang" AND CMAKE_BUILD_TYPE STREQUAL "Debug")
    foreach(PROTO_SRC ${ARGN})
      unset(PROTO_SRC_OPTIONS)
      get_source_file_property(PROTO_SRC_OPTIONS ${PROTO_SRC} COMPILE_OPTIONS)
      if(PROTO_SRC_OPTIONS)
        list(APPEND PROTO_SRC_OPTIONS "$<$<CONFIG:Debug>:-O2>")
      else()
        set(PROTO_SRC_OPTIONS "$<$<CONFIG:Debug>:-O2>")
      endif()

      set_source_files_properties(${PROTO_SRC} PROPERTIES COMPILE_OPTIONS "${PROTO_SRC_OPTIONS}")
    endforeach()
    unset(PROTO_SRC)
    unset(PROTO_SRC_OPTIONS)
  endif()
endfunction()

file(GLOB_RECURSE PROTO_DESC_LIST_PBDESC "${PROJECT_SERVER_FRAME_PROTOCOL_DIR}/pbdesc/*.proto")
file(GLOB_RECURSE PROTO_DESC_LIST_CONFIG "${PROJECT_SERVER_FRAME_PROTOCOL_DIR}/config/*.proto")
set(PROJECT_SERVER_FRAME_PROTO_GENERATED_DIR "${CMAKE_CURRENT_BINARY_DIR}/_generated/protocol")
set(PROJECT_SERVER_FRAME_PROTO_GENERATED_CONFIG_DIR "${PROJECT_SERVER_FRAME_PROTO_GENERATED_DIR}/config")
set(PROJECT_SERVER_FRAME_PROTO_GENERATED_PBDESC_DIR "${PROJECT_SERVER_FRAME_PROTO_GENERATED_DIR}/pbdesc")
file(MAKE_DIRECTORY "${PROJECT_SERVER_FRAME_PROTO_GENERATED_CONFIG_DIR}")
file(MAKE_DIRECTORY "${PROJECT_SERVER_FRAME_PROTO_GENERATED_PBDESC_DIR}")

# ============= configure protocols =============
set(PROJECT_SERVER_FRAME_PROTO_RELATIVE_DIR "${PROJECT_SERVER_FRAME_PROTOCOL_DIR}/config")
foreach(PROTO_FILE_FULL_PATH ${PROTO_DESC_LIST_CONFIG})
  file(RELATIVE_PATH PROTO_FILE ${PROJECT_SERVER_FRAME_PROTO_RELATIVE_DIR} ${PROTO_FILE_FULL_PATH})
  string(REGEX REPLACE "\\.proto$" "" PROTO_PATH_NAME ${PROTO_FILE_FULL_PATH})
  get_filename_component(PROTO_BASENAME "${PROTO_PATH_NAME}" NAME)
  set(PROTO_SRC "${PROTO_PATH_NAME}.pb.h" "${PROTO_PATH_NAME}.pb.cc")

  list(APPEND PROJECT_SERVER_FRAME_PROTO_FILES ${PROTO_FILE})
  list(APPEND PROJECT_SERVER_FRAME_PROTO_SRCS ${PROTO_SRC})
  list(APPEND PROJECT_SERVER_FRAME_PROTO_GENARATED_SRCS
       "${PROJECT_SERVER_FRAME_PROTO_GENERATED_CONFIG_DIR}/${PROTO_BASENAME}.pb.h"
       "${PROJECT_SERVER_FRAME_PROTO_GENERATED_CONFIG_DIR}/${PROTO_BASENAME}.pb.cc")
  list(APPEND SRC_LIST ${PROTO_SRC})
endforeach()

foreach(PROTO_GENARATED_SRC "pb_header_v3" "xrescode_extensions_v3")
  list(APPEND PROJECT_SERVER_FRAME_PROTO_GENARATED_SRCS
       "${PROJECT_SERVER_FRAME_PROTO_GENERATED_CONFIG_DIR}/${PROTO_GENARATED_SRC}.pb.h"
       "${PROJECT_SERVER_FRAME_PROTO_GENERATED_CONFIG_DIR}/${PROTO_GENARATED_SRC}.pb.cc")
  list(APPEND PROJECT_SERVER_FRAME_PROTO_SRCS "${PROJECT_SERVER_FRAME_PROTO_RELATIVE_DIR}/${PROTO_GENARATED_SRC}.pb.h"
       "${PROJECT_SERVER_FRAME_PROTO_RELATIVE_DIR}/${PROTO_GENARATED_SRC}.pb.cc")
endforeach()

add_custom_command(
  OUTPUT ${PROJECT_SERVER_FRAME_PROTO_SRCS} "${PROJECT_INSTALL_RES_PBD_DIR}/config.pb"
  COMMAND
    "${ATFRAMEWORK_CMAKE_TOOLSET_THIRD_PARTY_PROTOBUF_BIN_PROTOC}" --proto_path
    "${PROJECT_SERVER_FRAME_PROTO_RELATIVE_DIR}" --proto_path "${PROJECT_THIRD_PARTY_INSTALL_DIR}/include" --proto_path
    "${ATFRAMEWORK_LIBATBUS_REPO_DIR}/include" --proto_path "${ATFRAMEWORK_LIBATAPP_REPO_DIR}/include" --proto_path
    "${PROJECT_THIRD_PARTY_XRESLOADER_PROTO_DIR}" --proto_path
    "${PROJECT_THIRD_PARTY_XRESCODE_GENERATOR_REPO_DIR}/pb_extension" --cpp_out
    "${PROJECT_SERVER_FRAME_PROTO_GENERATED_CONFIG_DIR}" ${PROTO_DESC_LIST_CONFIG}
    "${PROJECT_THIRD_PARTY_XRESLOADER_PROTO_DIR}/pb_header_v3.proto"
    "${PROJECT_THIRD_PARTY_XRESCODE_GENERATOR_REPO_DIR}/pb_extension/xrescode_extensions_v3.proto"
  COMMAND "${CMAKE_COMMAND}" -E copy_if_different ${PROJECT_SERVER_FRAME_PROTO_GENARATED_SRCS}
          "${PROJECT_SERVER_FRAME_PROTO_RELATIVE_DIR}"
  COMMAND
    "${ATFRAMEWORK_CMAKE_TOOLSET_THIRD_PARTY_PROTOBUF_BIN_PROTOC}" --proto_path
    "${PROJECT_SERVER_FRAME_PROTO_RELATIVE_DIR}" --proto_path "${PROJECT_THIRD_PARTY_INSTALL_DIR}/include" --proto_path
    "${ATFRAMEWORK_LIBATBUS_REPO_DIR}/include" --proto_path "${ATFRAMEWORK_LIBATAPP_REPO_DIR}/include" --proto_path
    "${PROJECT_THIRD_PARTY_XRESLOADER_PROTO_DIR}" --proto_path
    "${PROJECT_THIRD_PARTY_XRESCODE_GENERATOR_REPO_DIR}/pb_extension" -o "${PROJECT_INSTALL_RES_PBD_DIR}/config.pb"
    ${PROTO_DESC_LIST_CONFIG} "${PROJECT_THIRD_PARTY_XRESLOADER_PROTO_DIR}/pb_header_v3.proto"
    "${PROJECT_THIRD_PARTY_XRESCODE_GENERATOR_REPO_DIR}/pb_extension/xrescode_extensions_v3.proto"
    "${ATFRAMEWORK_LIBATAPP_REPO_DIR}/include/atframe/atapp_conf.proto"
    "${ATFRAMEWORK_LIBATBUS_REPO_DIR}/include/libatbus_protocol.proto"
    "${PROJECT_THIRD_PARTY_INSTALL_DIR}/include/google/protobuf/duration.proto"
    "${PROJECT_THIRD_PARTY_INSTALL_DIR}/include/google/protobuf/timestamp.proto"
    "${PROJECT_THIRD_PARTY_INSTALL_DIR}/include/google/protobuf/descriptor.proto"
  WORKING_DIRECTORY ${PROJECT_SERVER_FRAME_PROTO_RELATIVE_DIR}
  DEPENDS ${PROTO_DESC_LIST_CONFIG}
  COMMENT "Generate [@${PROJECT_SERVER_FRAME_PROTO_RELATIVE_DIR}] ${PROJECT_SERVER_FRAME_PROTO_FILES}")
add_custom_target(
  protocol-config
  DEPENDS ${PROJECT_SERVER_FRAME_PROTO_SRCS}
  SOURCES ${PROJECT_SERVER_FRAME_PROTO_SRCS})
project_build_tools_patch_protobuf_sources(${PROJECT_SERVER_FRAME_PROTO_SRCS})
project_build_tools_optimize_sources(${PROJECT_SERVER_FRAME_PROTO_SRCS})

# ============= Convert excel =============
find_package(Java REQUIRED COMPONENTS Runtime)
file(GLOB PROJECT_RESOURCE_EXCEL_FILES "${PROJECT_SOURCE_DIR}/resource/excel/*.xlsx")
configure_file("${PROJECT_SOURCE_DIR}/resource/excel_xml/xresconv.xml.in" "${CMAKE_CURRENT_BINARY_DIR}/xresconv.xml"
               ESCAPE_QUOTES @ONLY)

set(PROJECT_RESOURCE_EXCEL_COMMAND_ARGS "${Python3_EXECUTABLE}" "${PROJECT_THIRD_PARTY_XRESLOADER_CLI}")
if (Java_JAVA_EXECUTABLE)
  list(APPEND PROJECT_RESOURCE_EXCEL_COMMAND_ARGS "--java-path" "${Java_JAVA_EXECUTABLE}")
endif()
list(APPEND PROJECT_RESOURCE_EXCEL_COMMAND_ARGS "${CMAKE_CURRENT_BINARY_DIR}/xresconv.xml")
add_custom_command(
  OUTPUT "${PROJECT_INSTALL_RES_DIR}/excel/const.bytes"
  COMMAND ${PROJECT_RESOURCE_EXCEL_COMMAND_ARGS}
  WORKING_DIRECTORY "${CMAKE_CURRENT_BINARY_DIR}"
  DEPENDS "${PROJECT_INSTALL_RES_PBD_DIR}/config.pb" ${PROJECT_RESOURCE_EXCEL_FILES}
  COMMENT "Generate excel resources [@${CMAKE_CURRENT_BINARY_DIR}]")

add_custom_target(resource-config ALL DEPENDS "${PROJECT_INSTALL_RES_DIR}/excel/const.bytes")

# ============= network protocols =============
unset(PROJECT_SERVER_FRAME_PROTO_FILES)
unset(PROJECT_SERVER_FRAME_PROTO_GENARATED_SRCS)
unset(PROJECT_SERVER_FRAME_PROTO_SRCS)
unset(PROJECT_SERVER_FRAME_PROTO_RELATIVE_DIR)
set(PROJECT_SERVER_FRAME_PROTO_RELATIVE_DIR "${PROJECT_SERVER_FRAME_PROTOCOL_DIR}/pbdesc")

foreach(PROTO_FILE_FULL_PATH ${PROTO_DESC_LIST_PBDESC})
  file(RELATIVE_PATH PROTO_FILE ${PROJECT_SERVER_FRAME_PROTO_RELATIVE_DIR} ${PROTO_FILE_FULL_PATH})
  string(REGEX REPLACE "\\.proto$" "" PROTO_PATH_NAME ${PROTO_FILE_FULL_PATH})
  get_filename_component(PROTO_BASENAME "${PROTO_PATH_NAME}" NAME)
  set(PROTO_SRC "${PROTO_PATH_NAME}.pb.h" "${PROTO_PATH_NAME}.pb.cc")

  list(APPEND PROJECT_SERVER_FRAME_PROTO_FILES ${PROTO_FILE})
  list(APPEND PROJECT_SERVER_FRAME_PROTO_SRCS ${PROTO_SRC})
  list(APPEND PROJECT_SERVER_FRAME_PROTO_GENARATED_SRCS
       "${PROJECT_SERVER_FRAME_PROTO_GENERATED_PBDESC_DIR}/${PROTO_BASENAME}.pb.h"
       "${PROJECT_SERVER_FRAME_PROTO_GENERATED_PBDESC_DIR}/${PROTO_BASENAME}.pb.cc")
  list(APPEND SRC_LIST ${PROTO_SRC})
endforeach()

add_custom_command(
  OUTPUT ${PROJECT_SERVER_FRAME_PROTO_SRCS}
  COMMAND
    "${ATFRAMEWORK_CMAKE_TOOLSET_THIRD_PARTY_PROTOBUF_BIN_PROTOC}" --proto_path
    "${PROJECT_SERVER_FRAME_PROTO_RELATIVE_DIR}" --proto_path "${PROJECT_THIRD_PARTY_INSTALL_DIR}/include" --proto_path
    "${ATFRAMEWORK_LIBATBUS_REPO_DIR}/include" --proto_path "${ATFRAMEWORK_LIBATAPP_REPO_DIR}/include" --cpp_out
    "${PROJECT_SERVER_FRAME_PROTO_GENERATED_PBDESC_DIR}" ${PROTO_DESC_LIST_PBDESC}
  COMMAND "${CMAKE_COMMAND}" -E copy_if_different ${PROJECT_SERVER_FRAME_PROTO_GENARATED_SRCS}
          "${PROJECT_SERVER_FRAME_PROTO_RELATIVE_DIR}"
  COMMAND
    "${ATFRAMEWORK_CMAKE_TOOLSET_THIRD_PARTY_PROTOBUF_BIN_PROTOC}" --proto_path
    "${PROJECT_SERVER_FRAME_PROTO_RELATIVE_DIR}" --proto_path "${PROJECT_THIRD_PARTY_INSTALL_DIR}/include" --proto_path
    "${ATFRAMEWORK_LIBATBUS_REPO_DIR}/include" --proto_path "${ATFRAMEWORK_LIBATAPP_REPO_DIR}/include" -o
    "${PROJECT_INSTALL_RES_PBD_DIR}/network.pb" ${PROTO_DESC_LIST_PBDESC}
    "${ATFRAMEWORK_LIBATAPP_REPO_DIR}/include/atframe/atapp_conf.proto"
    "${ATFRAMEWORK_LIBATBUS_REPO_DIR}/include/libatbus_protocol.proto"
    "${PROJECT_THIRD_PARTY_INSTALL_DIR}/include/google/protobuf/duration.proto"
    "${PROJECT_THIRD_PARTY_INSTALL_DIR}/include/google/protobuf/timestamp.proto"
    "${PROJECT_THIRD_PARTY_INSTALL_DIR}/include/google/protobuf/descriptor.proto"
  WORKING_DIRECTORY ${PROJECT_SERVER_FRAME_PROTO_RELATIVE_DIR}
  DEPENDS ${PROTO_DESC_LIST_PBDESC}
  COMMENT "Generate [@${PROJECT_SERVER_FRAME_PROTO_RELATIVE_DIR}] ${PROJECT_SERVER_FRAME_PROTO_FILES}")
add_custom_target(
  protocol-net
  DEPENDS ${PROJECT_SERVER_FRAME_PROTO_SRCS}
  SOURCES ${PROJECT_SERVER_FRAME_PROTO_SRCS})
project_build_tools_patch_protobuf_sources(${PROJECT_SERVER_FRAME_PROTO_SRCS})
project_build_tools_optimize_sources(${PROJECT_SERVER_FRAME_PROTO_SRCS})

unset(PROJECT_SERVER_FRAME_PROTO_FILES)
unset(PROJECT_SERVER_FRAME_PROTO_GENARATED_SRCS)
unset(PROJECT_SERVER_FRAME_PROTO_SRCS)
unset(PROJECT_SERVER_FRAME_PROTO_RELATIVE_DIR)

add_custom_target(protocol DEPENDS protocol-config protocol-net)
