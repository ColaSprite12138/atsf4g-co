aux_source_directory(${3RD_PARTY_LUA_SRC_DIR} 3RD_PARTY_LUA_ALL_SRC)
unset(3RD_PARTY_LUA_SRC_LIST)
foreach(3RD_PARTY_LUA_SRC_FILE IN LISTS 3RD_PARTY_LUA_ALL_SRC)
    get_filename_component(3RD_PARTY_LUA_SRC_FILE_BASENAME "${3RD_PARTY_LUA_SRC_FILE}" NAME)
    if (NOT "${3RD_PARTY_LUA_SRC_FILE_BASENAME}" STREQUAL "lua.c" AND 
        NOT "${3RD_PARTY_LUA_SRC_FILE_BASENAME}" STREQUAL "luac.c" AND
        NOT "${3RD_PARTY_LUA_SRC_FILE_BASENAME}" STREQUAL "onelua.c")
        list(APPEND 3RD_PARTY_LUA_SRC_LIST "${3RD_PARTY_LUA_SRC_FILE}")    
    endif ()
endforeach()

set(3RD_PARTY_LUA_EXPORT_NAME "${3RD_PARTY_LUA_LINK_NAME}-target")
add_library(${3RD_PARTY_LUA_LINK_NAME} STATIC 
    ${3RD_PARTY_LUA_SRC_LIST}
)

unset(3RD_PARTY_LUA_EXT_OPTIONS)

if (CMAKE_HOST_APPLE OR APPLE)
    list(APPEND 3RD_PARTY_LUA_EXT_OPTIONS LUA_USE_MACOSX)
elseif (CYGWIN OR MINGW)
    # do nothing
elseif (UNIX)
    list(APPEND 3RD_PARTY_LUA_EXT_OPTIONS LUA_USE_LINUX)
else ()
    # do nothing
endif ()

target_include_directories(${3RD_PARTY_LUA_LINK_NAME} 
    PUBLIC 
        "$<BUILD_INTERFACE:${3RD_PARTY_LUA_SRC_DIR}>"
        "$<INSTALL_INTERFACE:include>"
    # PRIVATE 
    #     "$<BUILD_INTERFACE:${3RD_PARTY_LUA_SRC_DIR}>"
)

target_compile_definitions(${3RD_PARTY_LUA_LINK_NAME}
    PRIVATE LUA_COMPAT_5_2 ${3RD_PARTY_LUA_EXT_OPTIONS}
)

if (MSVC)
    set_property(TARGET ${3RD_PARTY_LUA_LINK_NAME} PROPERTY FOLDER "3rd_party")
endif (MSVC)

include(GNUInstallDirs)
install(FILES "${3RD_PARTY_LUA_SRC_DIR}/lua.h" "${3RD_PARTY_LUA_SRC_DIR}/luaconf.h" "${3RD_PARTY_LUA_SRC_DIR}/lauxlib.h"
    TYPE INCLUDE
)

install(TARGETS ${3RD_PARTY_LUA_LINK_NAME}
    EXPORT ${3RD_PARTY_LUA_EXPORT_NAME}
    RUNTIME DESTINATION ${CMAKE_INSTALL_BINDIR}
    LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR}
    ARCHIVE DESTINATION ${CMAKE_INSTALL_LIBDIR}
)

export(TARGETS ${3RD_PARTY_LUA_LINK_NAME}
    FILE "${CMAKE_LIBRARY_OUTPUT_DIRECTORY}/cmake/${3RD_PARTY_LUA_EXPORT_NAME}.cmake"
)

install(
    EXPORT ${3RD_PARTY_LUA_EXPORT_NAME}
    DESTINATION "${CMAKE_INSTALL_LIBDIR}/cmake"
)
